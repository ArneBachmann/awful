# String operations
### string-from-list 1/1, string-concat 2/1, string-concat-list 2/1, string-split-at 2/2, string-len 1/1, string-insert-at 3/1, string-eq 2/1

include basicmaths
include lists


## Convert a list (of codepoints) into a string (via pushi instruction)
```
def string-from-list  1   # l
  as l                    #    l -> l

  def convert  1
    dup                   # i i
    l swap pull           # i l elem
    swapover              # elem l i
    pushi                 # l (updated)
    up ...l               #   l -> l
  end  0

  0 l.num 1               # l num
  &convert for            # (loop)
  3 l :type pushi         # convert type and leave string on stack
end  1
```
assert
  ""
  "abc"
  "54343/1000"
from
  [ ] string-from-list
  [ 97 98 99 ] string-from-list
  54.343 string-from-list
end


## Concatenate two strings
```
def string-concat  2
  list-concat
  string-from-list
end  1
```
assert
  "abcde"
from
  :ab "cde" string-concat
end


## Concatenate a list of strings
```
def string-concat-list  2
  as separator
  as list
  list list-len as remain
  list list-to-stack      # roll out entire list

  def concat'  0
    remain
    1 le if break
    separator swap string-concat string-concat
    remain 1 - up remain
    concat'
  end  ?

  concat'
end  1
```
assert
  "ab cd e"
from
  [ :ab :cd :e ] " " string-concat-list
end


## String splitting
```
def string-split-at  2    # string index
  list-split-at           # list1 list2
  string-from-list        # list1 s2
  swap                    # s2 list1
  string-from-list        # s2 s1
  swap                    # s1 s2
end  2
```
assert
  "abc"
  "de"
from
  "abcde" 3 string-split-at
end
assert "abcde" from "abcde" 2 string-split-at string-concat end


## Get string length
```
def string-len  1
  :num pull popnos
end  1
```
assert
  3 0 1
from
  "abc" string-len
  "" string-len
  :c string-len
end


## Insert a string at a position
```
def string-insert-at  3     # string index insert-string
  as toinsert
  as index
  index string-split-at     # prefix suffix
  swap toinsert string-concat
  swap string-concat
end  1
```
assert
  :axbc
  :xabc
  :abcx
from
  "abc" 1 :x string-insert-at
  "abc" 0 :x string-insert-at
  "abc" 3 :x string-insert-at
end


## Compare two strings
```
def string-eq  2  # s t
  as b as a
  a :num pull popnos as len
  b :num pull popnos
  len ne if break False

  def cmp  1
    as i
    a i pull popnos
    b i pull popnos
    ne if break False break'    # set result to False
  end  0
  0 len 1 &cmp forbreak
  is-nil
end  1  # boolean equality
```
assert
  False
  True
  False
  True
  False
  False
  False
from
  :a :ab  string-eq
  :ab :ab string-eq
  :xy :xz string-eq
  :x  :x  string-eq
  :x  :O  string-eq
  ""  " " string-eq
  " "  "" string-eq
end
