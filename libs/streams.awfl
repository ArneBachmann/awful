# Generic stream operations
### slurp 1/1, fprint 2/1, fprintln 2/1, print 1/0, eprint 1/0, println 1/0, eprintln 1/0


include lists    # list-append
include types


alias nl 10 chr end


## Get entire stream contents
```
def slurp  1        # stream
  as stream
  [ ] as result

  def read-loop  1
    read            # stream code|nil
    dup
    is-nil if break pop
    result swap list-append
    up .result
    read-loop       # loop
  end  1

  stream
  read-loop
  result string-from-list
end 2
```


## Print a string to a stream
```
def fprint  2         # stream text
  :num pull as len    #    len -> #code points
  as prompt
  as stream

  def echo  1         # index
    prompt swap pull  # prompt char-i
    popnos            # char-i
    stream swap write # output the character to the stream
    pop               # clean stack
  end  0

  0 len 1 &echo for
  stream              # leave stream on stack
end  1                # stream
```


## Print a string with a trailing newline character to a stream
```
def fprintln  2         # stream text
  nl string-concat      # append \n to string
  fprint
end  1                  # stream
```


def print  1
  stdout swap fprint pop
end  0


def println  1
  stdout swap fprintln pop
end  0


def eprint  1
  stderr swap fprint pop
end  0


def eprintln  1
  stderr swap fprintln pop
end  0


## Many related tests are performed in libs/files.awfl
